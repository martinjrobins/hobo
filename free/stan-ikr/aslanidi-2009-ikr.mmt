[[model]]
name: Aslanidi2009IKr
author: Michael Clerx
# Initial values
ikr.xr = 0.07

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membane potential
    in [mV]

[nernst]
EK = -85

[ikr]
use membrane.V
g = p8
IKr = g * xr * rr * (V - nernst.EK)
    in [uA/uF]
rr = 1 / (1 + exp((V + p6) / p7))
dot(xr) = (inf - xr) / tau
    tau = p1 / (1 + exp(V / p2)) + p3
    inf = 1 / (1 + exp(-(V + p4) / p5))
p1 = 900.0 [ms]
p2 = 5.0 [mV]
p3 = 100.0 [ms]
p4 = 0.085 [mV]
p5 = 12.25 [mV]
p6 = -5.4 [mV]
p7 = 20.4 [mV]
p8 = 0.04 [mS/uF]

